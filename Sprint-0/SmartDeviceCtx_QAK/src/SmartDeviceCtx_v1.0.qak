System smartdevice
	Request wasteDeposit : wasteDeposit(ID, Type, TruckLoad)
	Reply loadaccept : loadaccept(ID)
	Reply loadrejecetd : loadrejecetd(ID)
	
	Dispatch pickupOk : pickupOk(ID)
	
	
Context ctxwasteservice ip[host="127.0.0.1" port=8072]
Context ctxsmartdevice ip[host="localhost" port=8074]

ExternalQActor depositrequesthandler context ctxwasteservice



QActor smartdevice context ctxsmartdevice{
	[# var request_ID = 0 #]
	
	State s0 initial{
		//printCurrentMessage
		println("SmartDevice Started!")
	}
	Goto idle
	
	
	State idle {
		//printCurrentMessage
		println("Smart Device Waiting for Truck...")
		[# request_ID ++ #]
	}
	Transition t0 whenTime 10000 -> truckArrived
	
	State truckArrived {
		//printCurrentMessage
		println("Truck Arrived!")
		println("Sending request -$request_ID- :")
		[# val ID = request_ID #]
		
		request depositrequesthandler -m wasteDeposit : wasteDeposit($ID , 0, 10)
	}
	Transition t1 	whenTime  10000 -> truckArrived
					whenReply loadaccept -> waitPickUp
					whenReply loadrejecetd -> truckGoAway
					
					
	State waitPickUp {
		//printCurrentMessage
		println("Load Accepted -$request_ID- : waiting pickUp")
	}
	Transition t2 whenMsg pickupOk -> truckGoAway
	
	State truckGoAway {
		printCurrentMessage
		onMsg(loadrejecetd : loadrejecetd(ID)){
			println("Carico -${payloadArg(0)}- Rifiutato!")
		}
		
		onMsg(pickupOk : pickupOk(ID)){
			println("Scarico -${payloadArg(0)}- Completato!")
		}
		
		println("Il Truck puÃ² andare!")
	}
	Transition t3 whenTime 3000 -> idle
}